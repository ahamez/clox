cmake_minimum_required(VERSION 3.24)

if (CMAKE_SOURCE_DIR STREQUAL CMAKE_BINARY_DIR)
  message(
    FATAL_ERROR "Do not build in-source. Please remove CMakeCache.txt and the CMakeFiles directory,\
    then build out-of-source.")
endif ()

project(
  clox
  VERSION 0.1.0
  DESCRIPTION "A C++ Clox interpreter from the \"Crafting Interpreters\" book"
  LANGUAGES CXX)

string(TOUPPER "${PROJECT_NAME}" PROJECT_NAME_UPPER)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_VISIBILITY_PRESET hidden)
set(CMAKE_VISIBILITY_INLINES_HIDDEN ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
add_compile_options(-Wall -Wextra -Werror -Wpedantic)

include(GNUInstallDirs)
include(CheckIPOSupported)

check_ipo_supported(RESULT ipo_supported OUTPUT ipo_supported_result)
if (!ipo_supported)
  message(NOTICE "IPO is not supported")
  message(VERBOSE "IPO is not supported: ${ipo_supported_result}")
endif ()

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")

option(${PROJECT_NAME_UPPER}_CODE_COVERAGE "Activate code coverage" OFF)
option(${PROJECT_NAME_UPPER}_STATIC_ANALYSIS "Activate clang-tidy and cppcheck" OFF)
option(${PROJECT_NAME_UPPER}_IWYU " Activate include-what-you-use" OFF)

include(Asan)
if (${PROJECT_NAME_UPPER}_STATIC_ANALYSIS)
  include(StaticAnalysis)
endif ()
if (${PROJECT_NAME_UPPER}_IWYU)
  include(IWYU)
endif ()

# Support folders in IDEs
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Add Find*.cmake files generated by Conan.
list(APPEND CMAKE_MODULE_PATH ${CMAKE_BINARY_DIR})

find_package(Boost REQUIRED)
find_package(Catch2 REQUIRED)
find_package(fmt REQUIRED)
find_package(type_safe REQUIRED)
find_package(magic_enum REQUIRED)

include(CTest)
include(Catch)

if (${PROJECT_NAME_UPPER}_CODE_COVERAGE)
  include(CodeCoverage)
  append_coverage_compiler_flags()
  setup_target_for_coverage_lcov(
    NAME coverage
    EXECUTABLE ctest -j ${PROCESSOR_COUNT}
    DEPENDENCIES test_clox
    EXCLUDE /*.conan* /*llvm/* app/* test/*
  )
endif ()

install(
  EXPORT cloxTargets
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/clox
  NAMESPACE clox::
  FILE cloxConfig.cmake
)

add_subdirectory(app)
add_subdirectory(clox)
add_subdirectory(test)
